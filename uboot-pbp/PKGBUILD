# Maintainer: Nadia Holmquist Pedersen <nadia@nhp.sh>

pkgname=uboot-pbp
pkgver=2020.07rc5
_ubootver=$(echo "$pkgver" | sed "s/rc/-rc/")
_tfatag=v2.3
pkgrel=1
pkgdesc='U-boot for the Pinebook Pro.'
arch=('aarch64')
url='https://www.denx.de/wiki/U-Boot'
license=('GPL2')
install=${pkgname}.install
makedepends=('bc' 'make' 'gcc' 'dtc' 'uboot-tools' 'arm-none-eabi-gcc' 'python')

source=(
	"git+https://github.com/ARM-software/arm-trusted-firmware.git#tag=${_tfatag}"
	"ftp://ftp.denx.de/pub/u-boot/u-boot-${_ubootver}.tar.bz2"
	"0001-Add-regulator-needed-for-usage-of-USB.patch"
	"0002-Correct-boot-order-to-be-USB-SD-eMMC.patch"
	"0003-Enable-the-power-LED-during-early-startup.patch"
)
sha256sums=('SKIP'
            '9b2864a74d9848e90dbfa1833c185866e81293860ba6cab7ca88daaee85581fe'
            'b4f0c72489f58034587cf49654917e037a8ac9ecc6d6ad589cbc35fbdc6a1d74'
            '01f2389dec80b28024aa8f14133b62865dfac0285d0101ea0d7506639294e875'
            'fe82a9822c2c3ade6c5908df1b10b5102626c7bfcf59d8cdfc6f89aa33fd81c9')

prepare() {
	cd "${srcdir}/u-boot-${_ubootver}"

	# Patches almost entirely based on dhivael's u-boot repository at https://git.eno.space/pbp-uboot.git
	patch -Np1 -i "${srcdir}/0001-Add-regulator-needed-for-usage-of-USB.patch"
	patch -Np1 -i "${srcdir}/0002-Correct-boot-order-to-be-USB-SD-eMMC.patch"
	patch -Np1 -i "${srcdir}/0003-Enable-the-power-LED-during-early-startup.patch"
}

build() {
	unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

	make -C "${srcdir}"/arm-trusted-firmware PLAT=rk3399
	make -C "${srcdir}"/u-boot-${_ubootver} pinebook-pro-rk3399_defconfig
	make -C "${srcdir}"/u-boot-${_ubootver} BL31="${srcdir}"/arm-trusted-firmware/build/rk3399/release/bl31/bl31.elf
}

package() {
	cd "${srcdir}/u-boot-${_ubootver}"

	install -Dm 644 idbloader.img "${pkgdir}"/boot/idbloader.img
	install -Dm 644 u-boot.itb "${pkgdir}"/boot/u-boot.itb
}
