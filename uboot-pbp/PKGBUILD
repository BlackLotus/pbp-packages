# Maintainer: Nadia Holmquist Pedersen <nadia@nhp.sh>

pkgname=uboot-pbp
pkgver=2021.01
_ubootver=$(echo "$pkgver" | sed "s/rc/-rc/")
_tfatag=v2.4
pkgrel=1
pkgdesc='U-boot for the Pinebook Pro.'
arch=('aarch64')
url='https://www.denx.de/wiki/U-Boot'
license=('GPL2')
install=${pkgname}.install
makedepends=('bc' 'make' 'gcc' 'dtc' 'uboot-tools' 'arm-none-eabi-gcc' 'python')

_patches=(
	"0002-Correct-boot-order-to-be-USB-SD-eMMC.patch"
	"rk3399-Pinebook-pro-EDP-support.patch"
	"0005-PBP-Fix-Panel-reset.patch"
	"0003-Enable-the-power-LED-during-early-startup.patch"
)

source=(
	"git+https://github.com/ARM-software/arm-trusted-firmware.git#tag=${_tfatag}"
	"ftp://ftp.denx.de/pub/u-boot/u-boot-${_ubootver}.tar.bz2"
	config
	"${_patches[@]}"
)
sha256sums=('SKIP'
            'b407e1510a74e863b8b5cb42a24625344f0e0c2fc7582d8c866bd899367d0454'
            '19c29ea9dc2b9fbdc45262a557dd0e015deb7a8ec51b57bbb238e7215605f67d'
            '01f2389dec80b28024aa8f14133b62865dfac0285d0101ea0d7506639294e875'
            '61cb21ef106f85225a4c345860a602b248c4fcd0398c4f3c1f075ade669d4e98'
            'c3ea09a18b766a3ce0728234b097b29e2ed610c7f04b138b7fba42e118a7ae33'
            'fe82a9822c2c3ade6c5908df1b10b5102626c7bfcf59d8cdfc6f89aa33fd81c9')

prepare() {
	cd "${srcdir}/u-boot-${_ubootver}"

	# Patches almost entirely based on dhivael's u-boot repository at https://git.eno.space/pbp-uboot.git
	for patch in "${_patches[@]}"; do
		patch -Np1 -i "${srcdir}/$patch"
	done
}

build() {
	unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

	make -C "${srcdir}"/arm-trusted-firmware PLAT=rk3399
	#make -C "${srcdir}"/u-boot-${_ubootver} pinebook-pro-rk3399_defconfig
	cp "${srcdir}/config" "${srcdir}/u-boot-${_ubootver}/.config"
	make -C "${srcdir}"/u-boot-${_ubootver} BL31="${srcdir}"/arm-trusted-firmware/build/rk3399/release/bl31/bl31.elf
}

package() {
	cd "${srcdir}/u-boot-${_ubootver}"

	install -Dm 644 idbloader.img "${pkgdir}"/boot/idbloader.img
	install -Dm 644 u-boot.itb "${pkgdir}"/boot/u-boot.itb
}
