
pkgname=uboot-pinebookpro
pkgver=r62485.365495a329
pkgrel=1
pkgdesc='Almost upstream u-boot for the Pinebook Pro.'
arch=('aarch64')
url='https://git.eno.space/pbp-uboot.git'
license=('GPL2')
install=${pkgname}.install
makedepends=('bc' 'make' 'gcc' 'dtc' 'uboot-tools' 'arm-none-eabi-gcc' 'python')
replaces=('pinebookpro-uboot')

_tfatag=v2.3
source=(
	"git+https://github.com/ARM-software/arm-trusted-firmware.git#tag=${_tfatag}"
	"git+https://git.eno.space/pbp-uboot.git"
)
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}"/pbp-uboot
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
	export CFLAGS=-fcommon

	make -C "${srcdir}"/arm-trusted-firmware PLAT=rk3399
	make -C "${srcdir}"/pbp-uboot pinebook_pro-rk3399_defconfig
	make -C "${srcdir}"/pbp-uboot BL31="${srcdir}"/arm-trusted-firmware/build/rk3399/release/bl31/bl31.elf
}

package() {
	install -Dm 644 "${srcdir}"/pbp-uboot/idbloader.img "${pkgdir}"/boot/idbloader.img
	install -Dm 644 "${srcdir}"/pbp-uboot/u-boot.itb "${pkgdir}"/boot/u-boot.itb
}
